export optimal_traditional_de

"""
    optimal_traditional_de(s, method = "afnn", dmethod = "mi_min"; kwargs...) ‚Üí ùíü, œÑ, E

Produce an optimal delay embedding `ùíü` of the given timeseries `s` by
using the traditional approach of first finding an optimal (and constant) delay
time using [`estimate_delay`](@ref) with the given `dmethod`, and then an optimal
embedding dimension, by calculating an appropriate statistic for each dimension `d ‚àà 1:dmax`.
Return the embedding `ùíü`, the optimal delay time `œÑ`
(the optimal embedding dimension `d` is just `size(ùíü, 2)`) and the actual
statistic `E` used to estimate optimal `d`.

Notice that `E` is a function of the embedding dimension, which ranges from 1 to `dmax`.

For calculating `E` to estimate the dimension we use the given `method` which can be:

* `"afnn"` (default) is Cao's "Averaged False Nearest Neighbors" method[^Cao1997],
    which gives a ratio of distances between nearest neighbors.
* `"ifnn"` is the "Improved False Nearest Neighbors" from Hegger & Kantz[^Hegger1999],
    which gives the fraction of false nearest neighbors.
* `"fnn"` is Kennel's "False Nearest Neighbors" method[^Kennel1992], which gives
    the number of points that cease to be "nearest neighbors" when the dimension
    increases.
* `"f1nn"` is Krakovsk√°'s "False First Nearest Neighbors" method[^Krakovsk√°2015],
    which gives the ratio of pairs of points that cease to be "nearest neighbors"
    when the dimension increases.

For more details, see individual methods: [`delay_afnn`](@ref), [`delay_ifnn`](@ref),
[`delay_fnn`](@ref), [`delay_f1nn`](@ref).

!!! warn "Careful in automated methods"
    While this method is automated if you want to be **really sure** of the results,
    you should directly calculate the statistic and plot its values versus the
    dimensions.

## Keyword Arguments
The keywords
```julia
œÑs = 1:100, dmax = 10
```
denote which delay times and embedding dimensions `ds ‚àà 1:dmax` to consider when calculating
optimal embedding. The keywords 
```julia
slope_thres = 0.05, stoch_thres = 0.1, fnn_thres = 0.05
```
are specific to this function, see Description below.
All remaining keywords are propagated to the low level functions:
```
w, rtol, atol, œÑs, metric, r
```

## Description
We estimate the optimal embedding dimension based on the given delay time gained
from `dmethod` as follows: For Cao's method the optimal dimension is reached,
when the slope of the `E‚ÇÅ`-statistic (output from `"afnn"`) falls below the
threshold `slope_thres` and the according stochastic test turns
out to be false, i.e. if the `E‚ÇÇ`-statistic's first value is `< 1 - stoch_thres`.

For all the other methods we return the optimal embedding dimension
when the corresponding FNN-statistic (output from `"ifnn"`, `"fnn"` or `"f1nn"`)
falls below the fnn-threshold `fnn_thres` AND the slope of the
statistic falls below the threshold `slope_thres`. Note that with noise
contaminated time series, one might need to adjust `fnn_thres` according to the
noise level.

See also the file `test/compare_different_dimension_estimations.jl` for a comparison.

[^Cao1997]: Liangyue Cao, [Physica D, pp. 43-50 (1997)](https://www.sciencedirect.com/science/article/pii/S0167278997001188?via%3Dihub)

[^Kennel1992]: M. Kennel *et al.*, [Phys. Review A **45**(6), (1992)](https://journals.aps.org/pra/abstract/10.1103/PhysRevA.45.3403).

[^Krakovsk√°2015]: Anna Krakovsk√° *et al.*, [J. Complex Sys. 932750 (2015)](https://doi.org/10.1155/2015/932750)

[^Hegger1999]: Hegger & Kantz, [Improved false nearest neighbor method to detect determinism in time series data. Physical Review E 60, 4970](https://doi.org/10.1103/PhysRevE.60.4970).
"""
function optimal_traditional_de(s::AbstractVector, dimensionmethod::String = "afnn",
        delaymethod::String= "mi_min";
        fnn_thres::Real = 0.05, slope_thres::Real = .05, dmax::Int = 10, w::Int=1,
        rtol=10.0, atol=2.0, œÑs = 1:100, metric = Euclidean(), r::Real=2.0,
        stoch_thres = 0.1,
    )

    @assert dimensionmethod ‚àà ("afnn", "fnn", "ifnn", "f1nn")
    œÑ = estimate_delay(s, delaymethod, œÑs)
    ds = 1:dmax
    Œ≥s = ds .- 1 # TODO: This must be updated to dimension in 2.0

    if dimensionmethod=="afnn"
        dimension_statistic = delay_afnn(s, œÑ, ds; metric, w)
        Y, œÑ = find_cao_optimal(s, œÑ, dimension_statistic, slope_thres)
        flag = is_stochastic(s, œÑ, 1:3, stoch_thres)
        flag && println("Stochastic signal, valid embedding NOT achieved ‚®â.")
    elseif dimensionmethod=="fnn"
        dimension_statistic = delay_fnn(s, œÑ, ds; rtol, atol)
        Y, œÑ = find_fnn_optimal(s, œÑ, dimension_statistic, fnn_thres, slope_thres)
    elseif dimensionmethod=="ifnn"
        dimension_statistic = delay_ifnn(s, œÑ, ds; r, w, metric)
        Y, œÑ = find_fnn_optimal(s, œÑ, dimension_statistic, fnn_thres, slope_thres)
    elseif dimensionmethod=="f1nn"
        dimension_statistic = delay_f1nn(s, œÑ, ds; metric)
        Y, œÑ = find_fnn_optimal(s, œÑ, dimension_statistic, fnn_thres, slope_thres)
    end
    return Y, œÑ, dimension_statistic
end


"""
Helper function for selecting the appropriate embedding dimension from the
statistic when using Kennel's, Hegger's or Krakovskas's method.
"""
function find_fnn_optimal(s::AbstractVector{T}, œÑ::Int, rat::Vector, fnn_thres::Real,
                                                    slope_thres::Real) where {T}
    @assert length(rat) > 1
    y = abs.(diff(rat))
    flag = false
    m = 0
    for i = 2:length(rat)
        if rat[i] ‚â§ fnn_thres && y[i-1] ‚â§ slope_thres
            m = i
            break
        elseif rat[i] > rat[i-1]
            flag = true
            m = i-1
            break
        end
    end
    if m == length(rat) || m == 0
        println("Sufficiently small FNNs NOT reached. "*
                "Valid embedding NOT achieved ‚®â.")
    elseif flag
        println("Algorithm stopped due to increasing FNNs. "*
        "Double-check the FNN-statistic.")
    else
        println("Algorithm stopped due to sufficiently small FNNs. "*
                "Valid embedding achieved ‚úì.")
    end
    Y = embed(s, max(1, m), œÑ) # you can embed in 1 dimension in latest version
    return Y, œÑ
end


"""
Helper function for selecting the appropriate embedding dimension from the
statistic when using Cao's method.
"""
function find_cao_optimal(s::AbstractVector{T}, œÑ::Int, rat::Vector, thres::Real) where {T}
    m = 0
    y = abs.(diff(rat))
    for i = 1:length(rat)-1
        if y[i] ‚â§ thres && rat[i] > 0.5
            m = i
            break
        end
    end
    if m == 0
        println("NO convergence of E‚ÇÅ-statistic. "*
                "Valid embedding NOT achieved ‚®â.")
    else
        Y =  m > 1 ? embed(s, m, œÑ) : s
        println("Algorithm stopped due to convergence of E‚ÇÅ-statistic. "*
                "Valid embedding achieved ‚úì.")
    end
    Y = embed(s, max(1, m), œÑ) # you can embed in 1 dimension in latest version
    return Y, œÑ
end

"""
Helper function for Cao's method, whether to decide if the input signal is
stochastic or not.
"""
function is_stochastic(s, œÑ, ds, stoch_thres) where {T}
    E2 = stochastic_indicator(s, œÑ, ds)
    E2[1] > 1 - stoch_thres
end
